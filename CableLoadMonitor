#!/bin/sh

#
# DOCSIS cable load monitor 
#
# tool to monitor downstream load on DOCSIS cable networks
#
# https://github.com/sp4rkie/docsis-cable-load-monitor
#

export LD_PRELOAD=/opt/lib/libmediaclient.so

cat > $0_$$.awk << !

func setup( \
    )
{
    CLARGS = "$0|$1|$2|$3|$4|$5|$6|$7|$8|$9|${10}|${11}|${12}|${13}|${14}|${15}|${16}|${17}|${18}|${19}|${20}"

    # avoid dangling include files. incore inode still exists
    system("rm $0_$$.awk")

    # take care of shell expansion (if security is an issue)
    setup_lib($#, "awk|" CLARGS, $$, "`pwd`", "`hostname`", "$R_")
    PRP(CLARGS "\n")

    CABLE_MODEM_IP = "192.168.100.1"

    RRDBASE = INSTPATH "CableLoadMonitor.rrd"
    RRDBASE_RRASTEPS = 10                       # default to 10s

    RRDGRAPH = INSTPATH "CableLoadMonitor.png"
    RRDGRAPH_TMP = RRDGRAPH "_"
    RRDGRAPH_WIDTH = 1500                       # results in 1581x1111
    RRDGRAPH_HEIGHT = 800
    RRDGRAPH_HISTSTART = "now"                  # start from now
    RRDGRAPH_HISTSIZE = 3600                    # display the past 1h

    # ---
#   rrdbase_rrasteps = RRDBASE_RRASTEPS
    rrdgraph_histsize = RRDGRAPH_HISTSIZE
    rrdgraph_histstart = RRDGRAPH_HISTSTART
}

func exit_(msg \
    )
{
    PRE(msg)
    PRE("exiting...")
    ++ERR; exit
}

#
# specific to TC4400 yet
#
func retrieve_down_freqs( \
    URL, ignore, line, WGET, LYNX, i, a)
{
    URL = "http://" CABLE_MODEM_IP "/cmconnectionstatus.html"

    PRF("frequencies in use: ")
if (1) {
    WGET = "echo; wget --connect-timeout=1 -t 1 -q --http-user=admin --http-password=\"bEn2o#US9s\" " URL " -O -"
    LYNX = "lynx -nolist -width 300 -dump -stdin"
    while ((line = ex_line(WGET)) != -1) {
        if (match(line, "<script")) {
            ++ignore
        } else if (ignore) {
            if (match(line, "/script?")) {
                ignore = 0
            }
        } else {
            print line |& LYNX
        }
    }
    close(LYNX, "to")
    while (LYNX |& getline line > 0) {
        if (match(line, "Locked +SC-QAM +Downstream +Bonded +([0-9]+) ", a)) {
            FREQ[FREQ_CNT++] = a[1]
        }
    }
    close(LYNX)
} else {
    #
    # keep these for testing
    #
    FREQ[FREQ_CNT++] = "530000000"
    FREQ[FREQ_CNT++] = "538000000"
    FREQ[FREQ_CNT++] = "546000000"
    FREQ[FREQ_CNT++] = "554000000"
    FREQ[FREQ_CNT++] = "562000000"
    FREQ[FREQ_CNT++] = "570000000"
    FREQ[FREQ_CNT++] = "578000000"
    FREQ[FREQ_CNT++] = "586000000"
    FREQ[FREQ_CNT++] = "594000000"
    FREQ[FREQ_CNT++] = "602000000"
    FREQ[FREQ_CNT++] = "610000000"
    FREQ[FREQ_CNT++] = "618000000"
    FREQ[FREQ_CNT++] = "626000000"
    FREQ[FREQ_CNT++] = "634000000"
    FREQ[FREQ_CNT++] = "642000000"
    FREQ[FREQ_CNT++] = "650000000"
}
    if (!FREQ_CNT) {
        exit_("could not retrieve any")
    }
    PRF("[ " FREQ_CNT " ] ")
    for (i = 0; i < FREQ_CNT; ++i) {
        PRF(FREQ[i] / 1000000 " ")
        BWIDTH_OF[i] = "na"
    }
    PR("\n")
}

func assemble_rrdcreate_cmd( \
    CMD, i)
{
    CMD = "rrdtool create " RRDBASE " -s 1"                " \\\\\n"
    for (i = 0; i < FREQ_CNT; ++i) {
        CMD = CMD sprintf("DS:f%02d:GAUGE:120:U:U", i)     " \\\\\n"
    }
    CMD = CMD sprintf("DS:sum:GAUGE:120:U:U")              " \\\\\n"
    CMD = CMD sprintf("RRA:AVERAGE:0.1:" rrdbase_rrasteps ":86400")

    RRDCREATE_CMD = CMD
}

func assemble_rrdgraph_cmd( \
    CMD, i)
{
    CMD = "rrdtool graph " RRDGRAPH_TMP " -a PNG --lower-limit 0"                                      " \\\\\n" \
          "--title=\"Graph generated at \`date\`\""                                                    " \\\\\n" \
          "-w " RRDGRAPH_WIDTH " -h " RRDGRAPH_HEIGHT                                                  " \\\\\n" \
          "--end " rrdgraph_histstart " --start end-" rrdgraph_histsize "s"                            " \\\\\n" 
    CMD = CMD                                                                                          " \\\\\n"
    for (i = 0; i < FREQ_CNT; ++i) {
        CMD = CMD sprintf("DEF:f%02d=" RRDBASE ":f%02d:AVERAGE", i, i)                                 " \\\\\n" 
    }
    CMD = CMD sprintf("DEF:sum=" RRDBASE ":sum:AVERAGE", i, i)                                         " \\\\\n" 
    CMD = CMD                                                                                          " \\\\\n"
    for (i = 0; i < FREQ_CNT; ++i) {
        if (i < FREQ_CNT / 2) {
            CMD = CMD sprintf("\"LINE:f%02d#ff0000:%d\"", i, FREQ[i] / 1000000)                        " \\\\\n" 
        } else {
            CMD = CMD sprintf("\"LINE:f%02d#0000ff:%d\"", i, FREQ[i] / 1000000)                        " \\\\\n" 
        }
    }
    CMD = CMD                                                                                          " \\\\\n"
    CMD = CMD "\"LINE:sum#00ff00:sum\\\l\""                                                            " \\\\\n"
    CMD = CMD                                                                                          " \\\\\n"
    for (i = 0; i < FREQ_CNT; ++i) {
        CMD = CMD sprintf("\"GPRINT:f%02d:LAST:%d last\\\: %%6.0lf %%s\"", i, FREQ[i] / 1000000)       " \\\\\n"
        CMD = CMD sprintf("\"GPRINT:f%02d:AVERAGE:avg\\\: %%6.0lf %%s\"", i, FREQ[i] / 1000000)        " \\\\\n"
        CMD = CMD sprintf("\"GPRINT:f%02d:MIN:min\\\: %%6.0lf %%s\"", i, FREQ[i] / 1000000)            " \\\\\n"
        CMD = CMD sprintf("\"GPRINT:f%02d:MAX:max\\\: %%6.0lf %%s\\\j\"", i, FREQ[i] / 1000000)        " \\\\\n"
    }
    CMD = CMD sprintf("\"GPRINT:sum:LAST:sum last\\\: %%6.0lf %%s\"", i, FREQ[i] / 1000000)            " \\\\\n"
    CMD = CMD sprintf("\"GPRINT:sum:AVERAGE:avg\\\: %%6.0lf %%s\"", i, FREQ[i] / 1000000)              " \\\\\n"
    CMD = CMD sprintf("\"GPRINT:sum:MIN:min\\\: %%6.0lf %%s\"", i, FREQ[i] / 1000000)                  " \\\\\n"
    CMD = CMD sprintf("\"GPRINT:sum:MAX:max\\\: %%6.0lf %%s\\\j\"", i, FREQ[i] / 1000000)             #" \\\\\n"

    RRDGRAPH_CMD = CMD
}

func scan_down_channels( \
    RRDUPD_STR, BWIDTH_SUM, a, i, k)
{
    PRF(strftime("%T: "))
    for (i = 0; i < FREQ_CNT; ++i) {
        ex("timeout 10 dvbtune -f " FREQ[i] " -s 6952 -qam 256", 0, "Bit error rate: ([0-9]+)$", a)
        a[1] += 0
        if (a[1] && !dvbtune_ignerrs) {
            PR("\n\n" a[0] ", please check your cables\n")
        }

        #
        # in case of very small bandwidth values limit the measurement 
        # interval to something useful to keep the overall scan time sufficiently low
        #
        if (!ex("timeout 1 dvbsnoop -s bandwidth 8190 -n 400", 0, "^## PID:.*Avrg: +([^ ]+) ", a)) {
            exit_("dvbsnoop fails")
        }
        BWIDTH_OF[i] = int(a[1] + 0.5)
        PRF(sprintf("%6s", BWIDTH_OF[i]))
        BWIDTH_SUM = 0
        RRDUPD_STR = "rrdtool update " RRDBASE " N"
        for (k = 0; k < FREQ_CNT; ++k) {
            BWIDTH_SUM += BWIDTH_OF[k]
            RRDUPD_STR = RRDUPD_STR ":" BWIDTH_OF[k] * 1000
        }
        #
        # do not update until the initial scan completes
        #
        if (BWIDTH_OF[FREQ_CNT - 1] != "na") {
            RRDUPD_STR = RRDUPD_STR ":" BWIDTH_SUM * 1000
            ex(RRDUPD_STR, 2)
        } 
    }
    PR(sprintf("%7s", BWIDTH_SUM))
}

func generate_rrdgraph( \
    )
{
    ex(RRDGRAPH_CMD)
    ex("mv " RRDGRAPH_TMP " " RRDGRAPH, 2)
}

func watch_the_scenery( \
    )
{
    while (1) {
        generate_rrdgraph()
        scan_down_channels()
    }
}

func usage( \
    locked, line, a)
{
    PR("Usage: " PRGBNAME)
    while ((line = ex_line("cat " PRGNAME)) != -1) {
        if (locked) {
            if (match(line, "match[^,]+, \"(.+)\"[^\"]+# (.*)$", a)) {
                PR(sprintf("  %-20s - %s", a[1], a[2]))
            }
        } else {
            if (match(line, "^func process_cmdline")) ++locked
        }
    }
}

func process_cmdline( \
    i)
{
    for (i = 2; i < _ARGC; ++i) {
        if (match(_ARGV[i], "-h")) { # print this help and exit
            usage()
            exit
        } else if (match(_ARGV[i] " " _ARGV[i + 1], "-c +[0-9]+( |$)")) { # create a rrd base with given RRA steps
            rrdbase_rrasteps = _ARGV[i + 1]
            ++i
        } else if (match(_ARGV[i] " " _ARGV[i + 1], "-s +[0-9]+( |$)")) { # display time frame start in the past (in secs)
            rrdgraph_histstart = strftime("%s") - _ARGV[i + 1]
            ++i
        } else if (match(_ARGV[i] " " _ARGV[i + 1], "-w +[0-9]+( |$)")) { # display time frame size (in secs)
            rrdgraph_histsize = _ARGV[i + 1]
            ++i
        } else if (match(_ARGV[i], "-i")) { # ignore errors reported by dvbtune
            ++dvbtune_ignerrs
        } else {
            usage()
            ++ERR; exit
        }
    }
    if (!ex("ls " RRDBASE, 0 ,"^(" RRDBASE ")$")) {
        if (!rrdbase_rrasteps) rrdbase_rrasteps = RRDBASE_RRASTEPS
        PRF("no RRD base found: ")
    }
    if (rrdbase_rrasteps) PR("create a RRD base with RRA step size of " rrdbase_rrasteps)
    if (dvbtune_ignerrs) PR("dvbtune: " "ignore bit errors")
    PR("display time frame start: " (rrdgraph_histstart == "now" ? "now" : strftime("%T", rrdgraph_histstart)))
    PR("display time frame size:  " rrdgraph_histsize "s")
    
    retrieve_down_freqs()
    assemble_rrdcreate_cmd()
    assemble_rrdgraph_cmd()
    if (rrdbase_rrasteps) {
        ex(RRDCREATE_CMD, 2)
    }
    watch_the_scenery()
}

BEGIN {
    QUIET = 1
    setup()
    process_cmdline()
    exit
}

END { 
    cleanup()
    PRP("\n" PRGBNAME " " "exits with: " (ERR ? ERR : 0) " " "error(s)")
    PRP("====== [" sprintf(" %5d ", PROCINFO["pid"]) "] program stop  [" strftime() "] on " HOSTNAME " ======")
    close(PROTOCOL)
    exit ERR
}

!

[ -t 0 ] && STDIN="< /dev/null"
eval exec awk -f /usr/local/lib/l5.awklib -f $0_$$.awk $STDIN 2>&1

